import static org.gradle.internal.os.OperatingSystem.*

plugins {
    id 'java'
    id 'java-library'
    id "org.jetbrains.kotlin.jvm" version "1.3.70"
    id "com.github.johnrengelman.shadow" version '5.2.0'
}

// jitpack
group = 'com.github.kotlin_graphics'

ext{
    moduleName = "${group}.openvr"
    kotlin = 'org.jetbrains.kotlin:kotlin'
    kotlin_version = '1.3.70'
    kotlintest_version = '3.4.2'
    uno_version = '0cf66d34a0e2c94e1184528c2fa1bf23c15ff936'
    vkk_version = '326f36ec13234cc93056980f4fc59ab3b7c4574f'
    gln_version = '40443438c4a1d3018f96c80dba4488d47ea2c777'
    gli_version = 'aadbe89bf42f801c2ea55c5a38a2e748f7acdf14'
    glm_version = '1b4ac18dd1a3c23440d3f33596688aac60bc0141'
    unsigned_version = '18131d0fe0b7465a145a4502d31452c5ae0e59a1'
    kool_version = 'fcf04b2c03b8949d9d9a8b0a580082e927903510'
    lwjgl_version = "3.2.3"
    lwjgl_natives = current() == WINDOWS ? "windows" : current() == LINUX ? "linux" : "macos"
}

dependencies {

    implementation "$kotlin-stdlib-jdk8"

    testImplementation "io.kotlintest:kotlintest-runner-junit5:$kotlintest_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3'

    def kx = "com.github.kotlin-graphics"
    implementation "$kx:gln:$gln_version" // wtf testImplementation doesnt seem to work
    implementation "$kx:gli:$gli_version"
    implementation "${kx}.glm:glm:$glm_version"
    implementation "$kx:kool:$kool_version"
//    implementation "$kx:kotlin-unsigned:$unsigned_version"

//    implementation 'com.github.kotlin-graphics.uno-sdk:uno-core:683e68fbb3'
    ["core", "gl"].each {
        implementation "${kx}.uno-sdk:uno-$it:$uno_version"
    }

    ["", "-glfw", "-jemalloc", "-openal", "-opengl", "-stb", "-openvr", "-vulkan"].each {
        implementation "org.lwjgl:lwjgl$it:$lwjgl_version"
        if (it != "-vulkan")
            runtimeOnly "org.lwjgl:lwjgl$it:$lwjgl_version:natives-$lwjgl_natives"
    }

    implementation 'com.beust:klaxon:5.2'
}

repositories {
    mavenCentral()
    jcenter()
    maven { setUrl("https://oss.sonatype.org/content/repositories/snapshots/") }
    maven { setUrl('https://jitpack.io') }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
//    archives javadocJar
}

test.useJUnitPlatform()

// https://stackoverflow.com/a/47458666/1047713
//test {
//    testLogging {
//        events "passed", "skipped", "failed"
//        exceptionFormat "full"
//        outputs.upToDateWhen { false }
//        showStandardStreams = true
//    }
//}

shadowJar.archiveClassifier = 'all'

compileKotlin {
    // Enable Kotlin compilation to Java 8 class files with method parameter name metadata
    kotlinOptions {
        jvmTarget = "11"
//        javaParameters = true
    }
    // As per https://stackoverflow.com/a/47669720
    // See also https://discuss.kotlinlang.org/t/kotlin-support-for-java-9-module-system/2499/9
    destinationDir = compileJava.destinationDir
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
//        javaParameters = true
    }
}

compileJava {
    dependsOn(':compileKotlin')
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--patch-module', "$moduleName=${sourceSets["main"].output.asPath}"]
        classpath = files()
    }
}

jar { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }